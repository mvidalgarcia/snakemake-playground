configfile: "inputs.yaml"

rule all:
    input:
        "results/plot.png"

rule gendata:
    input:
        gendata="code/gendata.C"
    params:
        events=config["events"]
    output:
        "results/data.root"
    singularity:
        'docker://reanahub/reana-env-root6:6.18.04'
    shell:
        "mkdir -p results && "
        "root -b -q '{input.gendata}({params.events},\"{output}\")'"

rule fitdata:
    input:
        fitdata="code/fitdata.C",
        data="results/data.root"
    output:
        "results/plot.png"
    singularity:
        'docker://reanahub/reana-env-root6:6.18.04'
    shell:
        "root -b -q '{input.fitdata}(\"{input.data}\",\"{output}\")'"
